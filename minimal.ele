/*
From https://www.fordservicecontent.com/Ford_Content/vdirsnet/OwnerManual/Home/Content?bookCode=O192169&countryCode=USA&languageCode=en&marketCode=US&viewTech=IE&chapterTitleSelected=G2002375&subTitleSelected=G2048159&topicHRef=G2048160&div=f&variantid=7956&vFilteringEnabled=False&userMarket=GBR
Remote Control
Unlocking the Doors
Press the [open padlock/unlock] button to unlock the front doors. One long flash of the direction indicators confirms that your vehicle has unlocked.
Unlocking the Cargo Doors
Press the [van and open padlock] button on the remote control once to unlock the rear cargo doors. 
Press the [van and open padlock] button again within two seconds to unlock all cargo doors.
Press the [closed padlock] button twice within three seconds. [to double lock]
When you press the unlock button, either all the doors are unlocked or only the driver or driver and passenger doors are unlocked.
You can reprogram the unlocking function so that all doors are unlocked.
 Press and hold the unlock and lock buttons on the remote control simultaneously for at least four seconds with the ignition off.
*/

abstract sig Location{}

abstract sig Zone{
    locations: disj some Location
}

abstract sig Vehicle{
  //structural relations
    ,locations: some Location

    ,zones: some Zone
    ,locationsInZone: zones one -> some locations

    ,fob: disj one Fob

  //state
    ,var disj locked, unlocked: set zones
}{
  //facts about derived relations
    all z: zones | z.locations = locationsInZone[z]
  //facts about state
    zones = locked + unlocked
}

one sig DriverFront extends Location{}
one sig PassengerFront extends Location{}
one sig PassengerSide extends Location{}
one sig Rear extends Location{}

one sig People extends Zone{}
abstract sig Cargo extends Zone{}
one sig RearCargo extends Cargo{}
one sig OtherCargo extends Cargo{}

sig Transit extends Vehicle {
}{
   zones = 
         People 
       + RearCargo 
       + OtherCargo

   locations = 
         Rear
       + PassengerSide
       + DriverFront
       + PassengerFront

   locationsInZone = 
         People     -> DriverFront
       + People     -> PassengerFront
       + RearCargo  -> Rear
       + OtherCargo -> PassengerSide

    fob in TransitFob
}

//observations
pred Vehicle::zoneLocked[z: Zone]{
    z in this.locked
}

pred Vehicle::zoneUnchanged[z: Zone]{
    z & this.locked   = z & this.locked'
    z & this.unlocked = z & this.unlocked'
}

pred Vehicle::zonesOtherThanUnchanged[z: Zone]{
    all z: this.zones - z | this.zoneUnchanged[z]
}

pred Vehicle::otherVehiclesUnchanged{
   all v: Vehicle - this | v.unchanged
}

pred Vehicle::unchanged{
   this.locked'   = this.locked
   this.unlocked' = this.unlocked
}

//actions
pred Vehicle::unlock[z: Zone]{
    this.unlocked' = this.unlocked + z
    this.locked'   = this.locked   - z
    this.zonesOtherThanUnchanged[z]
    this.otherVehiclesUnchanged
    
}

pred Vehicle::lock[z: Zone]{
    this.locked'   = this.locked   + z
    this.unlocked' = this.unlocked - z
    this.zonesOtherThanUnchanged[z]
    this.otherVehiclesUnchanged
}

pred skip{
    all v: Vehicle |
        v.unchanged
}

abstract sig Button{}
one sig OpenPadlock, VanAndOpenPadlock, ClosedPadlock extends Button{}

abstract sig Fob{
    ,vehicle: disj one Vehicle
    ,buttons: some Button
}

sig TransitFob extends Fob{}{
    vehicle in Transit

    buttons =
            OpenPadlock
          + VanAndOpenPadlock
          + ClosedPadlock
}

//invariant frame conditions
assert aZoneIsLockedOrUnlocked{
   all v: Vehicle |
      no v.locked & v.unlocked
}

//event reification
//abstract sig Command {}
//one sig Unlock, Lock, Skip extends Command{}
enum VehicleAction{Unlock, CargoUnlock, Lock, Skip}
fun on: VehicleAction -> Vehicle -> Zone {
      Unlock -> {v: Vehicle, z: Zone| v.unlock[z]}
    + CargoUnlock -> {v:Vehicle, z:Zone| v.unlock[z]}
    + Lock   -> {v: Vehicle, z: Zone| v.lock[z]}
}

fun skip: VehicleAction {
    {c : Skip | skip}
}

fun commands: set VehicleAction {
    on.Zone.Vehicle + skip
}
    
//demonstration    
fact trace{
    //initally
    all v:Vehicle |
        all z: v.zones | v.zoneLocked[z]
    always
        (   skip
         or one v: Vehicle |
                 one z: Zone |
                       v.unlock[z]
                    or v.lock[z]
        )
}

pred allUnlocked{
    eventually
        all v:Vehicle | 
            all z: People| not v.zoneLocked[z]
}

run {allUnlocked} for exactly 2 Transit, 2 Fob, 8 Time

